// @strict-types

#Область ПрограммныйИнтерфейс

Процедура УстановитьЗаголовокБазыПоЗадаче(База, Задача) Экспорт

	Заголовок = ЗаголовокБазы(База, Задача);
	ЮрлБазы = ЮрлБазыСменыЗаголовка(База);
	ТелоЗапроса = Новый Структура("label", Заголовок);
	ДополнительныеПараметры = НовыйДополнительныеПараметры();
	Результат = КоннекторHTTP.PostJson(ЮрлБазы, ТелоЗапроса, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ЗаголовокБазы(База, Задача)
	
	НомерЗадачи = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Задача, "НомерЗадачи");
	ИмяБазы = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(База, "Наименование");
	
	Возврат СтрШаблон("[%1] %2", НомерЗадачи, ИмяБазы);
	
КонецФункции

Функция ЮрлБазыСменыЗаголовка(База)
	
	Подключение = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(База, "ИмяИнформационнойБазы, КластерСерверов.Наименование");
	Возврат СтрШаблон("http://%1/%2/hs/TW_DBE/change_label", 
		Подключение.КластерСерверовНаименование, Подключение.ИмяИнформационнойБазы);
	
КонецФункции

Функция НовыйДополнительныеПараметры()
	
	Аутентификация = Новый Структура("Пользователь, Пароль", "web", "web");
	
	ПараметрыПреобразования = Новый Структура;
	ПараметрыПреобразования.Вставить("ПрочитатьВСоответствие", Ложь);
	
	Возврат Новый Структура("Аутентификация, ПараметрыПреобразованияJSON", Аутентификация, ПараметрыПреобразования);
	
КонецФункции

#КонецОбласти