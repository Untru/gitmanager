#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Функция СпринтПоКоду(Код) Экспорт
	
	НайденнаяСсылка = НайтиПоКоду(Код);
	Если НайденнаяСсылка = ПустаяСсылка() Тогда
		НовыйЭлемент = СоздатьЭлемент();
		НовыйЭлемент.Код = Код;
		НовыйЭлемент.Записать();
		НайденнаяСсылка = НовыйЭлемент.Ссылка;
	КонецЕсли;
	Возврат НайденнаяСсылка;
	
КонецФункции

Функция ДанныеПоСпринтам(ПериодПоСпринтам = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Спринты.Ссылка КАК Ссылка,
	|	Спринты.Наименование КАК Наименование,
	|	Спринты.ДатаНачала КАК ДатаНачала,
	|	Спринты.ДатаОкончания КАК ДатаОкончания
	|ИЗ
	|	Справочник.Спринты КАК Спринты
	|ГДЕ
	|	Спринты.Ссылка = &Административные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Спринты.Ссылка,
	|	Спринты.Наименование,
	|	Спринты.ДатаНачала,
	|	Спринты.ДатаОкончания
	|ИЗ
	|	Справочник.Спринты КАК Спринты
	|ГДЕ
	|	Спринты.ДатаНачала >= &СпринтДатаНачала И Спринты.ДатаНачала <= &СпринтДатаОкончания
	|	И Спринты.Ссылка <> &Административные
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Спринты.Ссылка,
	|	Спринты.Наименование,
	|	ДАТАВРЕМЯ(2099, 1, 1),
	|	Спринты.ДатаОкончания
	|ИЗ
	|	Справочник.Спринты КАК Спринты
	|ГДЕ
	|	Спринты.Ссылка = &Неразобранно
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДатаНачала";
	
	Запрос.УстановитьПараметр("Административные", СпринтАдминистративные()); 
	Запрос.УстановитьПараметр("Неразобранно", СпринтНеразобранно());
	
	Если Не ЗначениеЗаполнено(ПериодПоСпринтам) Тогда
		ПериодПоСпринтам = Новый СтандартныйПериод(НачалоНедели(ТекущаяДатаСеанса()),
			КонецНедели(ТекущаяДатаСеанса()) + 86400 * 56
		);
	КонецЕсли;
	
	Запрос.УстановитьПараметр("СпринтДатаНачала", ПериодПоСпринтам.ДатаНачала); 
	Запрос.УстановитьПараметр("СпринтДатаОкончания", ПериодПоСпринтам.ДатаОкончания);
	
	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

Функция СпринтНеразобранно() Экспорт
	Возврат Справочники.Спринты.НайтиПоНаименованию("Неразобранно");
КонецФункции

Функция СпринтАдминистративные() Экспорт
	Возврат Справочники.Спринты.НайтиПоНаименованию("Административные");
КонецФункции

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	Поля.Добавить("Наименование");
	Поля.Добавить("ДатаНачала");
	Поля.Добавить("ДатаОкончания");
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 %2 - %3'"), Данные.Наименование,
		Формат(Данные.ДатаНачала, "ДФ=dd.MM"), Формат(Данные.ДатаОкончания, "ДФ=dd.MM")
	);
	
КонецПроцедуры

Функция ТекущийСпринт() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	Спринты.Ссылка КАК Спринт
	               |ИЗ
	               |	Справочник.Спринты КАК Спринты
	               |ГДЕ
	               |	&ТекущаяДата МЕЖДУ Спринты.ДатаНачала И Спринты.ДатаОкончания";
	
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Спринт;
	КонецЕсли;
	
	Возврат ПустаяСсылка();
	
КонецФункции

Функция СпринтПоКрайнемуСроку(КрайнийСрок) Экспорт
	
	Результат = ПустаяСсылка();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	Спринты.Ссылка КАК Спринт
	|ИЗ
	|	Справочник.Спринты КАК Спринты
	|ГДЕ
	|	Спринты.ДатаНачала < &ДатаНачала
	|	И Спринты.ДатаОкончания > &ДатаОкончания
	|	И Спринты.Наименование <> &Административные";
	Запрос.УстановитьПараметр("ДатаНачала", КрайнийСрок);
	Запрос.УстановитьПараметр("ДатаОкончания", КрайнийСрок);
	Запрос.УстановитьПараметр("Административные", СпринтАдминистративные());
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Результат = Выборка.Спринт;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли
