#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	База = Параметры.База;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВладелецФормы.ИмяФормы = "Обработка.МенеджерРелизов.Форма.Форма" Тогда
		КодРазрешения = "Обновление";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьКонфигуратор(Команда)
	ЗапускПриложения("config");	
	Закрыть();	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПредприятие(Команда)	
	ЗапускПриложения("enterprise");	
	Закрыть();	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ДанныеПоБазе(База)
	ДанныеПоБазе = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(База, "КластерСерверов.ИмяАгентаБазы, КластерСерверов.Наименование");
	Возврат ДанныеПоБазе;	
КонецФункции

&НаКлиенте
Процедура ЗапускПриложения(РежимЗапуска)
	
	ДанныеПоБазе = ДанныеПоБазе(База);
	
    ПараметрыЗапускаПрограммы = СтратегияЗапускаСкриптовВызовСервера.ПараметрыЗапускаПрограммы();
	ПараметрыЗапускаПрограммы.ПолучитьПотокВывода = Ложь;
	ПараметрыЗапускаПрограммы.ПолучитьПотокОшибок = Ложь;
	ПараметрыЗапускаПрограммы.ИмяФайлаПотокаВывода = "runner";
	ПараметрыЗапускаПрограммы.ИмяФайлаПотокаОшибок = "runnerError";
	
	ПараметрыЗапускаПрограммы.СтрокаКоманды = СтрШаблон(ШаблонСтрокиЗапуска(),
		РежимЗапуска,
		ДанныеПоБазе.КластерСерверовНаименование,
		ДанныеПоБазе.КластерСерверовИмяАгентаБазы,
		КодРазрешения,
		Логин,
		Пароль
	);
	
	ЗапускПриложенийЧерезCMDКлиент.ЗапускПриложения(ПараметрыЗапускаПрограммы);
	
КонецПроцедуры

&НаКлиенте
Функция ШаблонСтрокиЗапуска()
	Возврат """C:\Program Files\1cv8\common\1cestart.exe"" %1 /s %2\%3 /L RU /UC ""%4"" /N ""%5"" /P ""%6""";	
КонецФункции

#КонецОбласти